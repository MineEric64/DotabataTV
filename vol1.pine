//@version=6
indicator(title="DOTABATA! Collection Vol.1", shorttitle="DOTABATA!", precision=2, timeframe="", timeframe_gaps=true)
import TradingView/ta/11
//Stochastic + MACD

// Getting inputs
fast_length = input(title = "Fast Length", defval = 12)
slow_length = input(title = "Slow Length", defval = 26)
src = input(title = "Source", defval = close)
signal_length = input.int(title = "Signal Smoothing",  minval = 1, maxval = 50, defval = 9, display = display.data_window)
sma_source = input.string(title = "Oscillator MA Type",  defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
sma_signal = input.string(title = "Signal Line MA Type", defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
// Calculating
fast_ma = sma_source == "SMA" ? ta.sma(src, fast_length) : ta.ema(src, fast_length)
slow_ma = sma_source == "SMA" ? ta.sma(src, slow_length) : ta.ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)
hist = macd - signal

fixed_range = input.bool(false, "Use Stochastic fixed range", "If the indicator is not visible, you should disable it. Then, using dynamic range based on MACD highest value.")
fixed_range_value = input.int(1, "Stochastic fixed range value", minval=1, tooltip="Increase the value when Stochastic is not visible.")
show_bounds = input.bool(true, "Show Stochastic Bounds", "Limit Line")

alertcondition(hist[1] >= 0 and hist < 0, title = 'Rising to falling', message = 'The MACD histogram switched from a rising to falling state')
alertcondition(hist[1] <= 0 and hist > 0, title = 'Falling to rising', message = 'The MACD histogram switched from a falling to rising state')

//Fixed Range
h0 = hline(fixed_range ? 0.6 * fixed_range_value : na, "Upper Band (Fixed)", color=#787B86)
h1 = hline(fixed_range ? -0.6 * fixed_range_value : na, "Lower Band (Fixed)", color=#787B86)
h0b = hline(fixed_range and show_bounds ? fixed_range_value : na, "Upper Limit (Fixed)", color=color.red)
h1b = hline(fixed_range and show_bounds ? fixed_range_value * -1 : na, "Lower Limit (Fixed)", color=color.red)
fill(h0, h1, color=color.rgb(33, 150, 243, 90), title="Background")

//Dynamic Range
max_range_raw = ta.highest(macd, 300)
max_range = max_range_raw < 1 ? 1 : max_range_raw
h00 = plot(fixed_range ? na : 0.6 * max_range, title="Upper Band (Dynamic)", style=plot.style_circles, color=#787B86)
h11 = plot(fixed_range ? na : -0.6 * max_range, title="Lower Band (Dynamic)", style=plot.style_circles, color=#787B86)
h000 = plot(fixed_range or not show_bounds ? na : max_range, title="Upper Limit (Dynamic)", style=plot.style_circles, color=color.red)
h111 = plot(fixed_range or not show_bounds ? na : max_range * -1, title="Lower Limit (Dynamic)", style=plot.style_circles, color=color.red)
fill(h00, h11, color=color.rgb(33, 150, 243, 90), title="Background")

hline(0, "Zero Line", color = color.new(#787B86, 50))
plot(hist, title = "Histogram", style = plot.style_columns, color = (hist >= 0 ? (hist[1] < hist ? #26A69A : #B2DFDB) : (hist[1] < hist ? #FFCDD2 : #FF5252)))
plot(macd,   title = "MACD",   color = #AF23E7, linewidth=2)
plot(signal, title = "Signal", color = #54596A, linewidth=2)

periodK = input.int(14, title="%K Length", minval=1)
smoothK = input.int(1, title="%K Smoothing", minval=1)
periodD = input.int(3, title="%D Smoothing", minval=1)
k = ta.sma(ta.stoch(close, high, low, periodK), smoothK)
d = ta.sma(k, periodD)

k2 = ((k / 100) * 2 - 1) * (fixed_range ? fixed_range_value : max_range)
d2 = ((d / 100) * 2 - 1) * (fixed_range ? fixed_range_value : max_range)

plot(k2, title="%K", color=#2962FF)
plot(d2, title="%D", color=#FF6D00)
